# Inefficient allocation in `depositAndAllocateForPartyB()`: Low allocation amounts for PartyB

## Summary
The function `depositAndAllocateForPartyB()` is intended to deposit and allocate funds for PartyB but performs the allocation incorrectly. Due to missing conversion to 18 decimals before allocation, the resulting allocation amounts are significantly lower than expected.
## Vulnerability Detail
We can see the correct implementation, when depositing funds for partyA that the function `deposit` in 6 DEC (internally it converts it to 18) and converts to 18 DEC afterwards so it can `allocate()` in 18DEC
```jsx
    function depositAndAllocate(
        uint256 amount
    ) external whenNotAccountingPaused notLiquidatedPartyA(msg.sender) {
        AccountFacetImpl.deposit(msg.sender, amount);
        uint256 amountWith18Decimals = (amount * 1e18) /
        (10 ** IERC20Metadata(GlobalAppStorage.layout().collateral).decimals());
        AccountFacetImpl.allocate(amountWith18Decimals);
        emit Deposit(msg.sender, msg.sender, amount);
        emit AllocatePartyA(msg.sender, amountWith18Decimals);
    }
```
But the function `depositAndAllocateForPartyB()`, does not convert to 18 DEC before allocation, leading to low allocation amounts:
```jsx
    function depositAndAllocateForPartyB(
        uint256 amount,
        address partyA
    ) external whenNotPartyBActionsPaused onlyPartyB {
        AccountFacetImpl.depositForPartyB(amount);
        AccountFacetImpl.allocateForPartyB(amount, partyA, true);
        emit DepositForPartyB(msg.sender, amount);
        emit AllocateForPartyB(msg.sender, partyA, amount);
    }
```
Example:
 - partyB calls `depositAndAllocateForPartyB()` with 1000USDC
 - 1000USDC gets deposited and converted to 18DEC
 - 1000USDC gets allocated in 6 DEC

 The balance for this partyB is **1000e18**, but the allocated amount is only **1000e6**, which is inefficient to do anything
## Impact
Wrong function implementation, leading to low allocations for certain party.
## Code Snippet
[Account/AccountFacet.sol#L74-L82](https://github.com/sherlock-audit/2023-06-symmetrical/blob/main/symmio-core/contracts/facets/Account/AccountFacet.sol#L74-L82)
## Tool used

Manual Review

## Recommendation
Add the conversion to 18 DEC
```jsx
    function depositAndAllocateForPartyB(
        uint256 amount,
        address partyA
    ) external whenNotPartyBActionsPaused onlyPartyB {
        AccountFacetImpl.depositForPartyB(amount);
 -      AccountFacetImpl.allocateForPartyB(amount, partyA, true);
 +      uint256 amountWith18Decimals = (amount * 1e18) / 
 +      (10 ** IERC20Metadata(GlobalAppStorage.layout().collateral).decimals());
 +      AccountFacetImpl.allocateForPartyB(amountWith18Decimals, partyA, true);
        emit DepositForPartyB(msg.sender, amount);
        emit AllocateForPartyB(msg.sender, partyA, amount);
    }
```